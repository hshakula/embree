## Copyright 2009-2020 Intel Corporation
## SPDX-License-Identifier: Apache-2.0

FUNCTION(ADD_COMMON_LIBRARY NAME)
    SET(multiValueArgs
        PUBLIC_CLASSES
        PRIVATE_CLASSES
        PUBLIC_HEADERS
        PRIVATE_HEADERS
        LIBRARIES
    )

    CMAKE_PARSE_ARGUMENTS(args
        ""
        ""
        "${multiValueArgs}"
        ${ARGN}
    )

    SET(${NAME}_PUBLIC_HEADERS ${args_PUBLIC_HEADERS})
    SET(${NAME}_SOURCES ${args_PUBLIC_HEADERS} ${args_PRIVATE_HEADERS})
    FOREACH(class ${args_PUBLIC_CLASSES})
        LIST(APPEND ${NAME}_SOURCES ${class}.h ${class}.cpp)
        LIST(APPEND ${NAME}_PUBLIC_HEADERS ${class}.h)
    ENDFOREACH()
    FOREACH(class ${args_PRIVATE_CLASSES})
        LIST(APPEND ${NAME}_SOURCES ${class}.h ${class}.cpp)
    ENDFOREACH()

    ADD_LIBRARY(${NAME} STATIC ${${NAME}_SOURCES})
    TARGET_LINK_LIBRARIES(${NAME} PUBLIC ${args_LIBRARIES})

    TARGET_COMPILE_DEFINITIONS(${NAME} PUBLIC ${GLOBAL_DEFINITIONS})
    SET_PROPERTY(TARGET ${NAME} PROPERTY FOLDER common)
    SET_PROPERTY(TARGET ${NAME} APPEND PROPERTY COMPILE_FLAGS " ${FLAGS_LOWEST}")

    IF (EMBREE_INSTALL_COMMON)
      SET_TARGET_PROPERTIES(${NAME} PROPERTIES PUBLIC_HEADER "${${NAME}_PUBLIC_HEADERS}")
      INSTALL(TARGETS ${NAME} EXPORT ${NAME}-targets ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT devel PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/embree3/common/${NAME})
      INSTALL(EXPORT ${NAME}-targets DESTINATION ${EMBREE_CMAKEEXPORT_DIR} COMPONENT devel)
    ENDIF()
ENDFUNCTION(ADD_COMMON_LIBRARY)

ADD_SUBDIRECTORY(sys)
ADD_SUBDIRECTORY(math)
ADD_SUBDIRECTORY(simd)
ADD_SUBDIRECTORY(lexers)
ADD_SUBDIRECTORY(tasking)
ADD_SUBDIRECTORY(algorithms)
